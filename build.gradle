plugins {
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

ext {
    groupId = 'nice.fontaine'
    artifactId = 'overpass-wrapper'
    libVersion = '0.0.1'
}

group groupId
version libVersion

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "com.squareup.retrofit2:retrofit:2.1.0"
    compile "com.squareup.retrofit2:converter-moshi:2.1.0"
    compile "com.squareup.okhttp3:okhttp:3.4.1"
    compile "com.squareup.moshi:moshi:1.5.0"
    compile "com.squareup.moshi:moshi-adapters:1.5.0"

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:2.5.0"
}


bintray {
    user = '' //BINTRAY_USER
    key = '' //BINTRAY_KEY
    publications = ['mavenJava']

    pkg {
        repo = artifactId
        name = artifactId
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/wolfhardfehre/overpass-wrapper.git'
        version {
            name = libVersion
            desc = 'A Java wrapper library around the Overpass QL to query the Overpass API.'
            released = new Date()
        }
    }
}

task sourcesJar(type: Jar, dependsOn: project.classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId artifactId
            from components.java

            artifact sourcesJar {
                classifier = 'sources'
            }
            artifact javadocJar {
                classifier = 'javadoc'
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport
